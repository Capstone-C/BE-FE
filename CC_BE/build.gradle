plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.capstone'
version = '0.0.1-SNAPSHOT'
description = 'CC_BE'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // --- Spring Boot 기본 기능 ---
    implementation 'org.springframework.boot:spring-boot-starter-web'         // 웹 서버
    implementation 'org.springframework.boot:spring-boot-starter-security'    // 인증/인가
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'    // JPA/DB
    implementation 'org.springframework.boot:spring-boot-starter-validation'  // 입력값 검증
    implementation 'org.springframework.boot:spring-boot-starter-actuator'    // 모니터링/헬스체크
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'  // Swagger UI

    // HTML Sanitizer
    implementation 'org.jsoup:jsoup:1.18.1'

    // --- 개발 편의/도구 ---
    compileOnly 'org.projectlombok:lombok'                                   // 코드 자동 생성
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'     // 도커 연동

    // --- DB 드라이버 ---
    runtimeOnly 'com.mysql:mysql-connector-j'                                 // MySQL

    // --- 테스트 ---
    testImplementation 'org.springframework.boot:spring-boot-starter-test'    // 테스트
    testImplementation 'org.springframework.security:spring-security-test'     // 시큐리티 테스트
    testImplementation 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'              // JUnit 런처

    // --- JWT ---
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // --- OCR (Tesseract + OpenCV) ---
    implementation 'net.sourceforge.tess4j:tess4j:5.9.0'                     // OCR 엔진
    implementation 'org.openpnp:opencv:4.9.0-0'                              // 이미지 전처리 (노이즈 제거, 이진화 등)
}

tasks.named('test') {
    useJUnitPlatform()
    
    // JNA 네이티브 라이브러리 경로 설정 (Tesseract용)
    systemProperty 'jna.library.path', '/opt/homebrew/lib'
    
    // OpenCV 네이티브 라이브러리 경로 설정
    doFirst {
        def opencvNativeDir = file("${buildDir}/opencv-native")
        opencvNativeDir.mkdirs()
        
        // OpenCV JAR에서 네이티브 라이브러리 추출
        configurations.testRuntimeClasspath.files.each { file ->
            if (file.name.contains('opencv')) {
                copy {
                    from zipTree(file)
                    into opencvNativeDir
                    include 'nu/pattern/opencv/**/*.dylib'  // macOS
                    include 'nu/pattern/opencv/**/*.so'     // Linux
                    include 'nu/pattern/opencv/**/*.dll'    // Windows
                }
            }
        }
        
        // java.library.path 설정
        def nativeLibPath = fileTree(opencvNativeDir) {
            include '**/*.dylib', '**/*.so', '**/*.dll'
        }.files.collect { it.parentFile.absolutePath }.unique().join(File.pathSeparator)
        
        systemProperty 'java.library.path', nativeLibPath
    }
    
    // Tesseract 설정 (로컬 설치 경로)
    environment 'TESSDATA_PREFIX', '/opt/homebrew/share/tessdata'  // Homebrew 기본 경로
    
    // 테스트 로그 상세 출력
	testLogging {
		events "passed", "skipped", "failed"
		exceptionFormat "full"
		showStandardStreams = false  // Set to true for debugging OCR output
	}
}

// 생성되는 Boot JAR 이름 고정
bootJar {
    archiveFileName = 'cc-be.jar'
}

// Ensure Java compiler retains parameter names for reflection-based frameworks (e.g., Spring Data @Query named params)
tasks.withType(JavaCompile).configureEach {
    if (!options.compilerArgs.contains('-parameters')) {
        options.compilerArgs += '-parameters'
    }
}
